! print_rg.inp
!    Analyze and print radius of gyration restraint statistics
!
! ***********************************************************************
! * Copyright 2003-2018 Alexandre Bonvin, Utrecht University.           *
! * All rights reserved.                                                *
! * This code is part of the HADDOCK software and governed by its       *
! * license. Please see the LICENSE file that should have been included *
! * as part of this package.                                            *
! ***********************************************************************
!

@RUN:protocols/initialize.cns(iteration=$iteration;)

@NEWIT:iteration.cns(iteration=$iteration;)

@RUN:run.cns(
iteration =$iteration;
filenames =$filenames;
data      =$data;
iterations=$iterations;
saprotocol=$saprotocol;
refine    =$refine;
toppar    =$toppar;
analysis  =$analysis;
)

!reads in structure, coordinates and parameter files
@RUN:protocols/read_struc.cns
@RUN:protocols/read_struc-cg.cns

@PREVIT:file.cns(filenames=$filenames)
evaluate ($file = $filenames.bestfile_1)
coor @@$file

delete sele=(resn TIP* or resn WAT or resn HOH or resn DMS* or resn ANI or resn DAN or resn XAN or resn SHA) end
delete sele=(not(known)) end

set display=NEWIT:analysis/rg.disp end
display # Rg analysis
display # filename	Rg

evaluate ($sum_rg = 0)
evaluate ($sumsq_rg = 0)

@PREVIT:file.cns(filenames=$filenames)

evaluate ($count = 0)
evaluate ($TotalNstruct = 0)
while ($count lt $Iterations.Structures) loop main
   evaluate ($count=$count+1)
   evaluate ($file = $filenames.bestfile_$count)
   if ($file ne "") then
      evaluate ($TotalNstruct = $TotalNstruct + 1)
   end if
end loop main

if ($Iterations.anastruc > $TotalNstruct) then
  evaluate ($Iterations.anastruc = $ToTalNstruct)
end if

evaluate ($count = 1)
evaluate ($nstruc1 = 0)
while ($count le $Iterations.anastruc) loop main
   evaluate ($file = $filenames.bestfile_$count)
   coor init end
   if ($file ne "") then
     coor @@$file
     evaluate ($nstruc1 = $nstruc1 + 1)
     coor rgyration sele=(all) end
     evaluate ($sum_rg = $sum_rg + $rg)
     evaluate ($sumsq_rg = $sumsq_rg + $rg**2)
     display # $file $rg  
   end if
   if ($count ge $Iterations.anastruc) then exit loop main end if
   evaluate ($count = $count + 1)
end loop main

evaluate ($mean_rg = $sum_rg / $nstruc1)
evaluate ($stdev_rg = sqrt(($sumsq_rg - $nstruc1*$mean_rg**2)/ $nstruc1))

display # mean values all
display # Rg $mean_rg (+/- $stdev_rg )

stop
