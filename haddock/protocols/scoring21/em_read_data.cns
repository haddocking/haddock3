!module (Data;)
    {*
    Module reads in the cryoEM data, supposed to be the CryoEM map. Since CNS
    is mainly build for X-ray Crystallography and thus reciprocal space,
    working in real space is a bit cumbersome. Therefor we first determine the
    map size and such befor reading it in. The unit cell is determined first
    and the resolution cutoff.  After that the FFT parameters can be determined
    and space is generated for the reflections. 

    It follows with reading in the form factors for all the atoms. The arrays
    FOBS, FCALC, FPART and MAP are allocated. The CryoEM map is read and the
    target function is set, which is here the so called vector potential. The
    vector potential is the reciprocal space variant of the cross correlation
    between two maps.

    Parameters
    ----------
    Data : Data object
        The Data object holds all the parameter for the CryoEM functionality

    Notes
    -----
    After the call of this module one is able to use the XREF energy flag at will.

    Examples
    --------
    >>> ! Use the XREF energy term
    !>>> @RUN:protocols/em_read_data.cns(Data=&Data;)
    >>> flags include XREF end
    *}

    xray
        ! Set unit cell parameters. The angles are implicitely assumed to be 90'
        symmetry=(x, y, z)
        a=$Data.em.xlength
        b=$Data.em.ylength
        c=$Data.em.zlength

        ! set target resolution
        mapresolution $Data.em.resolution

        ! set up FFT
        method=FFT
        evaluate($voxelspacing = $Data.em.xlength / $Data.em.nx)
        evaluate($gridsize = $voxelspacing/$Data.em.resolution)
        fft 
            automemory=true
            gridsize=$gridsize
            bscale=100
            elimit=9
            prime=5
        end

        ! allocate space for the reflections.
        generate 100000 $Data.em.resolution
    end

    xray
        ! read in atomic form factors
        @@$scatter_lib

        ! reciprocal space complex arrays
        declare name=fobs  domain=reciprocal type=complex end
        declare name=fcalc domain=reciprocal type=complex end
        declare name=fpart domain=reciprocal type=complex end

        ! real space arrays
        declare name=map domain=real end

        ! only calculate structure factors for protein and DNA
        associate fcalc (not (name H* or resn HOH or resn TIP3 or resn DAN or resn XAN or resn DUM))

        ! read in map
        read map
            to=map
            input=RUN:data/cryo-em/cryo-em.xplor
            formatted=True
        end

        ! set initial values
        do (fobs = ft(map)) (all)
        do (fcalc = 0) (all)
        do (fpart = 0) (all)

        ! set which Fourier coefficients are used for XREF term and which
        ! are used for cross-validation
        tselection=(all)
        cvselection=(not all)

        ! set target function and target derivative function for XREF energy
        target =( vector(combine(amplitude(fobs),phase(fobs)), (fcalc), 1))
        dtarget=(dvector(combine(amplitude(fobs),phase(fobs)), (fcalc), 1))

        ! set weight of target function
        wa=$Data.em.kscale

        ! set tolerance level for recomputing structure factors. This 
        ! should maybe be higher during the refinement stage.
        tolerance=0.0
    end
