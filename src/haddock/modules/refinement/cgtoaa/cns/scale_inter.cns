! scale_inter.cns
!    Scaling of intermolecular interactions
!
! ***********************************************************************
! * Copyright 2003-2022 Alexandre Bonvin, Utrecht University.           *
! * All rights reserved.                                                *
! * This code is part of the HADDOCK software and governed by its       *
! * license. Please see the LICENSE file that should have been included *
! * as part of this package.                                            *
! ***********************************************************************
!
igroup interaction (not all) (not all) end

igroup

    eval($nchain1 = 0)

    while ($nchain1 < $data.ncomponents) loop nloop1
        eval($nchain1 = $nchain1 + 1)
  
        interaction (segid $prot_segid_$nchain1 and not (resn ANI or resn DAN or resn XAN or resn DUM or resn SHA)) 
                    (segid $prot_segid_$nchain1 and not (resn ANI or resn DAN or resn XAN or resn DUM or resn SHA)) weight * $scale.int_$nchain1_$nchain1 end
        interaction (segid $prot_segid_$nchain1 and not (resn ANI or resn DAN or resn XAN or resn DUM or resn SHA)) 
                    ((resn WAT or resn HOH or resn TIP* or resn DMS)) weight * 1.0 vdw $kinter elec $kinter end

        eval($nchain2 = $nchain1 )
        while ($nchain2 < $data.ncomponents) loop nloop2
            eval($nchain2 = $nchain2 + 1)
            eval($scalfac = $kinter * $scale.int_$nchain1_$nchain2)
            interaction (segid $prot_segid_$nchain1 and not (resn ANI or resn DAN or resn XAN or resn DUM or resn SHA))
                        (segid $prot_segid_$nchain2 and not (resn ANI or resn DAN or resn XAN or resn DUM or resn SHA)) weight * 1.0 vdw $scalfac elec $scalfac end
        end loop nloop2

    end loop nloop1

    interaction (resn ANI) (resn ANI) weight * 1.0 vdw 0.0 elec 0.0 end
    interaction (resn DAN) (resn DAN) weight * 1.0 vdw 0.0 elec 0.0 end
    interaction (resn XAN) (resn XAN) weight * 1.0 vdw 0.0 elec 0.0 end
    interaction (resn DUM) (resn DUM) weight * 1.0 vdw 0.0 elec 0.0 end
    interaction (resn SHA) (resn SHA) weight * 1.0 vdw 0.0 elec 0.0 end
    interaction (resn SHA) (not resn SHA) weight * 0.0 end

    interaction ((resn WAT or resn HOH or resn TIP* or resn DMS)) ((resn WAT or resn HOH or resn TIP* or resn DMS)) weight * 1.0 end

end
