! sa_ltad_cool1.cns
!    Perform the first simulated annealing stage of it1
!
! ***********************************************************************
! * Copyright 2003-2022 Alexandre Bonvin, Utrecht University.           *
! * Originally adapted from Aria 1.2 from Nilges and Linge, EMBL.       *
! * All rights reserved.                                                *
! * This code is part of the HADDOCK software and governed by its       *
! * license. Please see the LICENSE file that should have been included *
! * as part of this package.                                            *
! ***********************************************************************
!

evaluate ($ncycle = 10)

evaluate ($interstep = ($SaProtocol.inter_fin_rigid - $SaProtocol.inter_init_rigid)/($ncycle-1))
evaluate ($kinter = $SaProtocol.inter_init_rigid)

evaluate ($asy_add = ($Data.asy_cool1-$Data.asy_hot)/$ncycle)
evaluate ($masy_add = ($Data.masy_cool1-$Data.masy_hot)/$ncycle)

evaluate ($asy=$Data.asy_hot)
evaluate ($masy=$Data.masy_hot)
noe
    rswitch ambi $Data.Rswi_hot
    rswitch dist $Data.Rswi_hot
    rswitch hbon $Data.Rswi_hot

    mrswitch ambi $Data.mRswi_hot
    mrswitch dist $Data.mRswi_hot
    mrswitch hbon $Data.mRswi_hot

    asym ambi $Data.asy_hot
    asym dist $Data.asy_hot
    asym hbon $Data.asy_hot

    masym ambi $Data.masy_hot
    masym dist $Data.masy_hot
    masym hbon $Data.masy_hot
end

restraints dihedral   scale=$Data.dihedrals_cool1   end


evaluate ($tempstep = ($SaProtocol.t1_init - $SaProtocol.t1_final)/$ncycle)
evaluate ($calcsteps = int($SaProtocol.cool1_steps/$ncycle))
evaluate ($printsteps = int($calcsteps / 4))
evaluate ($timestep = $SaProtocol.timestep*$SaProtocol.tadfactor)

if ($Data.unambig_hot = 0) then
    evaluate ($Data.unambig_hot = 0.000001)
end if
if ($Data.unambig_cool1 = 0) then
    evaluate ($Data.unambig_cool1 = 0.000001)
end if
if ($Data.ambig_hot = 0)then
    evaluate ($Data.ambig_hot = 0.000001)
end if
if ($Data.ambig_cool1 = 0) then
    evaluate ($Data.ambig_cool1 = 0.000001)
end if
if ($Data.hbond_hot = 0) then
    evaluate ($Data.hbond_hot = 0.000001)
end if
if ($Data.hbond_cool1 = 0) then
    evaluate ($Data.hbond_cool1 = 0.000001)
end if

evaluate ($k_unamb= $Data.unambig_hot)
evaluate ($unambig_fac = ($Data.unambig_cool1/$Data.unambig_hot)^(1/$ncycle))
evaluate ($k_amb= $Data.ambig_hot)
evaluate ($ambig_fac = ($Data.ambig_cool1/$Data.ambig_hot)^(1/$ncycle))
evaluate ($k_hbond= $Data.hbond_hot)
evaluate ($hbond_fac = ($Data.hbond_cool1/$Data.hbond_hot)^(1/$ncycle))


evaluate ($bath  = $SaProtocol.t1_init)
do (vx=maxwell($bath)) ( all )
do (vy=maxwell($bath)) ( all )
do (vz=maxwell($bath)) ( all )



evaluate ($reassign = TRUE)
evaluate ($i_cool = 0)
while ($i_cool < $ncycle) loop cool

    evaluate ($i_cool=$i_cool+1)

    evaluate ($bath  = $bath  - $tempstep)
    evaluate ($k_unamb = $k_unamb*$unambig_fac)
    evaluate ($k_amb = $k_amb*$ambig_fac)
    evaluate ($k_hbond = $k_hbond*$hbond_fac)
    evaluate ($asy=$asy+$asy_add)
    evaluate ($masy=$masy+$masy_add)

    noe
        scale dist $k_unamb
        scale ambi $k_amb
        scale hbon $k_hbond
        asym ambi $asy
        asym dist $asy
        asym hbon $asy
        masym ambi $masy
        masym dist $masy
        masym hbon $masy
    end

    !scale intermolecular interactions
    inline @MODULE:scale_inter.cns
    evaluate ($kinter = $kinter + $interstep)

    dyna tors
       reassign $reassign
       timestep=$timestep
       nstep=$calcsteps
       nprint=$printsteps
       ntrfrq=0
       tcoupling = true  temperature = $bath
    end

    energy end
    if ($ener > 99999999) then
        display ENERGY PROBLEM, THE SYSTEM IS BLOWING UP: ==> STOPPING
        stop
    end if
    evaluate ($reassign = FALSE)
    
end loop cool

